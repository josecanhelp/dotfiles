" ------------------------------------------------------------------------------
" # Vimrc
" ------------------------------------------------------------------------------
" # This file sets up my basic vim environment.  Here I set a few defaults,
" # paths, and source the rest of the config files as necessary.  Most
" # of the config lives in plugins.vim and mappings.vim, with more
" # elaborate config in /local-config and /plugin-config.


" # Defaults
" ------------------------------------------------------------------------------
" Vim 8 defaults
" For more info, see :help defaults.vim
unlet! skip_defaults_vim
silent! source $VIMRUNTIME/defaults.vim


" ------------------------------------------------------------------------------
" # Paths
" ------------------------------------------------------------------------------
" Parent paths
let g:dotfiles_path = $HOME . '/.dotfiles'
let g:dotvim_path = $HOME . '/.vim'

" Get path relative to .vimrc file
function! VimrcPath(path)
  return g:dotfiles_path . '/vim/' . a:path
endfunction

" Get path relative to .vim directory
function! DotVimPath(path)
  return g:dotvim_path . '/' . a:path
endfunction

" Vimrc related paths for autocmds
let g:vimrc_related_paths = [
  \ $MYVIMRC,
  \ VimrcPath('vimrc'),
  \ VimrcPath('*.vim')
  \ ]


" ------------------------------------------------------------------------------
" # Sourcing
" ------------------------------------------------------------------------------
" Source plugins
call plug#begin('~/.vim/plugged')
  execute 'source ' . VimrcPath('plugins.vim')
call plug#end()

" Source mappings
execute 'source ' VimrcPath('mappings.vim')

" Setup function for dynamic sourcing
function! s:source_config_files_in(folder)
  for config_file in split(glob(VimrcPath(a:folder . '/*')), '\n')
    if filereadable(config_file)
      execute 'source' config_file
    endif
  endfor
endfunction

" Auto sourcing
augroup autosource_vimrc
  autocmd!
  execute 'autocmd BufWritePost ' . join(g:vimrc_related_paths, ',') . ' nested source ' . $MYVIMRC
augroup END



" ------------------------------------------------------------------------------
" # Misc. & Management
" ------------------------------------------------------------------------------
autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif

" Modeline and Notes {{{
" vim:foldmethod=marker:foldlevel=0
"
"	This is the personal .vimrc file of Jose Soto.
"	Website: JoseSoto.com
" }}}
